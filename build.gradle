/*
 * Copyright 2017 ThoughtWorks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'java'
    id 'com.github.breadmoirai.github-release' version '2.4.1'
}

group = 'cd.go'
version = '2.1.2'

ext {
    pluginId = 'cd.go.authorization.keycloak'
    pluginVersion = project.version
    goCdVersion = '19.2.0'
    pluginName = 'Keycloak oauth authorization plugin'
    pluginDescription = 'Keycloak oauth authorization plugin for GoCD'
    vendorName = 'klinux'
    vendorUrl = 'https://github.com/klinux/gocd-keycloak-oauth-authorization-plugin'
}

repositories {
    mavenCentral()
    mavenLocal()
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

ext {
    deps = [
            gocdPluginApi: 'cd.go.plugin:go-plugin-api:23.3.0',
    ]

    versions = project.ext.deps.collectEntries { lib, libGav -> [lib, libGav.split(':').last()] }
}

dependencies {
    compileOnly project.deps.gocdPluginApi
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.10.1'
    implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: '4.11.0'

    testImplementation project.deps.gocdPluginApi
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '2.2.28'
    testImplementation group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3'
    testImplementation group: 'org.skyscreamer', name: 'jsonassert', version: '1.4.0'
    testImplementation group: 'org.jsoup', name: 'jsoup', version: '1.10.2'
    testImplementation group: 'com.squareup.okhttp3', name: 'mockwebserver', version: '4.11.0'

    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

tasks.withType(Jar) { jarTask ->
    preserveFileTimestamps = false
    reproducibleFileOrder = true

    ['MD5', 'SHA1', 'SHA-256'].each { algo ->
        jarTask.outputs.files("${jarTask.archiveFile.get()}.${algo}")
        jarTask.doLast {
            ant.checksum file: jarTask.archiveFile.get(), format: 'MD5SUM', algorithm: algo
        }
    }

    manifest {
        attributes(
                'Go-Version': project.ext.goCdVersion,
                'Plugin-Revision': project.ext.pluginVersion,
                'Implementation-Title': project.ext.pluginName,
                'Implementation-Version': project.ext.pluginVersion,
                'Source-Compatibility': project.java.sourceCompatibility,
                'Target-Compatibility': project.java.targetCompatibility
        )
    }
}

configurations {
    testImplementation.extendsFrom compileOnly
}

sourceSets {
    main {
        resources {
            srcDirs = ['src/main/resources', 'src/main/resources-generated']
        }
    }
}

jar {
    archiveBaseName.set('keycloak-oauth-authorization-plugin')
    from(configurations.runtimeClasspath) {
        into "lib/"
    }
}

githubRelease {
    def repoSlug = System.getenv('GITHUB_REPOSITORY')?.split('/') ?: ['klinux', 'gocd-keycloak-oauth-authorization-plugin']
    owner = repoSlug[0]
    repo = repoSlug.size() > 1 ? repoSlug[1] : 'gocd-keycloak-oauth-authorization-plugin'
    token = System.getenv('GITHUB_TOKEN') ?: ''
    tagName = "v${project.version}"
    releaseName = "v${project.version}"
    targetCommitish = 'main'
    prerelease = !"No".equalsIgnoreCase(System.getenv('PRERELEASE'))
    releaseAssets = jar.outputs.files
}
